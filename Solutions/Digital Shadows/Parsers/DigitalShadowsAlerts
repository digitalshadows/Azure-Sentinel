// Usage Instruction : 
// Paste below query in log analytics, click on Save button and select as Function from drop down by specifying function name and alias (e.g. DigitalShadowsAlerts).
// Function usually takes 10-15 minutes to activate. You can then use function alias from any other queries (e.g. DigitalShadowsAlerts | take 10).
// References : 
// Using functions in Azure monitor log queries : https://docs.microsoft.com/azure/azure-monitor/log-query/functions
// Tech Community Blog on KQL Functions : https://techcommunity.microsoft.com/t5/Azure-Sentinel/Using-KQL-functions-to-speed-up-analysis-in-Azure-Sentinel/ba-p/712381
//
DigitalShadows_CL
| extend EventVendor = "Digital Shadows",
         EventProduct = "SearchLight",
         Type = "DigitalShadows_CL",
         EventStartTime = raised_t,
         EventMessage = title_s,
         EventOriginalUid = triage_id_g,
         EventOriginalType = classification_s
| extend EventOriginalSeverity = iif(isempty(risk_level_s), risk_assessment_risk_level_s, risk_level_s) 
| extend EventSeverity = case(EventOriginalSeverity == 'none', 'Informational',
                              EventOriginalSeverity == 'very-low', 'Low',
                              EventOriginalSeverity == 'low', 'Low',
                              EventOriginalSeverity == 'medium', 'Medium',
                              EventOriginalSeverity == 'high', 'High',
                              EventOriginalSeverity == 'very-high', 'High',
                              'Informational')
| extend EventReportUrl = iif(isempty(id_d),
                              strcat('https://portal-digitalshadows.com/triage/alerts/',portal_id_s),
                              strcat('https://portal-digitalshadows.com/triage/alert-incidents/',id_d))
| extend AdditionalFields = pack("assets", assets_s,
                                 "comments", comments_s,
                                 "description", description_s,
                                 "incident_id", id_d,
                                 "alert_id", id_g,
                                 "short_code", portal_id_s,
                                 "impact", impact_description_s,
                                 "mitigation", mitigation_s,
                                 "risk_factors", risk_factors_s,
                                 "triage_status", status_s,
                                 "triage_id", triage_id_g,
                                 "triage_raised", triage_raised_time_t,
                                 "triage_updated", triage_updated_time_t,
                                 "updated", updated_t)
 | project TimeGenerated,
           EventVendor,
           EventProduct,
           Type,
           EventStartTime,
           EventMessage,
           EventOriginalUid,
           EventOriginalType,
           EventOriginalSeverity,
           EventSeverity,
           EventReportUrl,
           AdditionalFields
| extend description = AdditionalFields.description
| extend impact = AdditionalFields.impact
| extend mitigation = AdditionalFields.mitigation
| extend status = AdditionalFields.triage_status
| extend comments = AdditionalFields.comments
